/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
    id 'jacoco'
}

description = 'Ballerina - AI Package Compiler Plugin Tests'

dependencies {
    checkstyle project(':checkstyle')
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstylePluginVersion}"

    testImplementation project(":ai-compiler-plugin")
    testImplementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${ballerinaLangVersion}"
    testImplementation group: 'org.ballerinalang', name: 'ballerina-parser', version: "${ballerinaLangVersion}"
    testImplementation group: 'org.ballerinalang', name: 'ballerina-tools-api', version: "${ballerinaLangVersion}"
    testImplementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
}

tasks.withType(Checkstyle) {
    exclude '**/module-info.java'
}

checkstyle {
    toolVersion "${project.checkstylePluginVersion}"
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile": file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

checkstyleTest.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")

spotbugsTest {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
    def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.LOW
    reportsDir = file("$project.buildDir/reports/spotbugs")
    reports {
        html.enabled true
        text.enabled = true
    }
    def excludeFile = file("${rootDir}/spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        excludeFilter = excludeFile
    }
}

spotbugsMain {
    enabled false
}

checkstyleMain {
    enabled false
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
    reportsDirectory = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
    sourceSets project(':ai-compiler-plugin').sourceSets.main
}

def distributionVersion = stripBallerinaExtensionVersion(ballerinaLangVersion)
def testResources = "${project.projectDir}/src/test/resources"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")

task updateTomlFiles {
    doLast {
        def updateTomlFile = { File file ->
            def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/BallerinaTest.toml")
            def newBallerinaTomlContent = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
            newBallerinaTomlContent = ballerinaTomlFilePlaceHolder.text.replace("@toml.version@", tomlVersion)
            file.text = newBallerinaTomlContent
        }
        fileTree(testResources) { include '**/Ballerina.toml' }
                .each { File ballerinaToml -> updateTomlFile(ballerinaToml) }
    }
}

test {
    dependsOn(updateTomlFiles)
    systemProperty "ballerina.offline.flag", "true"
    systemProperty "ballerina.distribution.version", "ballerina-${distributionVersion}"
    useTestNG()

    testLogging {
        exceptionFormat = "full"
        showStandardStreams = true
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Compiler Plugin Tests: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

test.dependsOn ":ai-ballerina:build"
compileTestJava.dependsOn ":ai-compiler-plugin:copyOpenApiJar"
test.dependsOn ":ai-ballerina:publishAiPackageToLocal"
test.finalizedBy jacocoTestReport
jacocoTestReport.dependsOn test
